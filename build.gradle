plugins {
    id 'java'
    id 'org.springframework.boot' version "${SPRING_BOOT_VERSION}"
    id 'io.spring.dependency-management' version "${SPRING_BOOT_DEPENDENCY_MANAGEMENT_VERSION}"
}

group = 'com.loopon'
version = '0.0.1-SNAPSHOT'
description = 'loop-on-backend'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(Integer.parseInt(JAVA_VERSION))
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // MySQL Driver
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Liquibase
    implementation 'org.springframework.boot:spring-boot-starter-liquibase'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()

    if (project.hasProperty('includeTags')) {
        def tags = project.property('includeTags').toString()
                .split(',')
                .collect { it.trim() }
                .findAll { it }
        useJUnitPlatform {
            includeTags(tags as String[])
        }
    }

    if (project.hasProperty('excludeTags')) {
        def tags = project.property('excludeTags').toString()
                .split(',')
                .collect { it.trim() }
                .findAll { it }
        useJUnitPlatform {
            excludeTags(tags as String[])
        }
    }
}
